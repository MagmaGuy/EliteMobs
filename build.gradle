plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

//Fixes issues related to special characters being used
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

group 'com.magmaguy'
version '7.3.3'

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()

    //Spigot API & CloudCommands
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    //Bstats API
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    //CloudCommands
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    //Vault
    maven { url = 'https://jitpack.io' }
    //DiscordSRV
    maven { url = 'https://m2.dv8tion.net/releases/'}
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    //LibsDisguises
    maven { url = 'https://repo.md-5.net/content/groups/public/' }
    //PlaceholderAPI
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }

    //WorldGuard API
    maven { url = 'https://maven.enginehub.org/repo/' }

}

dependencies {
    //Spigot API
    compileOnly "org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT"
    //BStats API
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '2.2.1'
    //WorldGuard API
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard-bukkit', version: '7.0.0'
    //CloudCommands
    implementation group: 'cloud.commandframework', name: 'cloud-paper', version: '1.4.0'
    //CloudCommands Minecraft Extras module
    implementation group: 'cloud.commandframework', name: 'cloud-minecraft-extras', version: '1.4.0'
    //CloudCommands Adventure Platform module
    implementation group: 'net.kyori', name: 'adventure-platform-bukkit', version: '4.0.0-SNAPSHOT'
    //I can't remember what this was for but it's in CloudCommands
    implementation group: 'io.leangen.geantyref', name: 'geantyref', version: '1.3.4'
    //Vault
    compileOnly group: 'com.github.MilkBowl', name: 'VaultAPI', version: '1.7'
    //DiscordSRV
    compileOnly 'net.dv8tion:JDA:4.2.1_262'
    implementation group: 'com.discordsrv', name: 'discordsrv', version: '1.23.0'
    //SQLite
    compileOnly group: 'org.xerial', name: 'sqlite-jdbc', version: '3.32.3'
    //LibsDisguises
    compileOnly group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.7.0';
    compileOnly group: 'LibsDisguises', name: 'LibsDisguises', version: '10.0.26'
    //PlaceholderAPI
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.10.9'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    //apache commons
    //implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

}

artifacts { // task 'build' runs generates uberjar
    archives shadowJar
}

jar {
    archiveClassifier.set('min') // we want the Uberjar to be distributed, this is the minified version
}

String packagePath = 'com.magmaguy.shaded'

// Relocating a Package
shadowJar {
    dependencies {
        relocate('org.bstats.bukkit', packagePath + '.bstats')

        relocate('cloud.commandframework', packagePath + '.cloud')
        relocate('net.kyori', packagePath + '.adventure-platform-bukkit')
        relocate('io.leangen.geantyref', packagePath + '.geantyref')
    }
    archiveClassifier.set(null)
    archiveFileName.set(project.name + ".jar")
    destinationDirectory.set(new File("testbed/plugins"))
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

ext {
    resourceTokens = [
            'Version': version
    ]
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

wrapper {
    gradleVersion = "7.0"
    distributionType = Wrapper.DistributionType.BIN
}
